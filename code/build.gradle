import org.jetbrains.dokka.gradle.DokkaTask

buildscript {
    ext {
        artyUser = findProperty('ARTIFACTORY_USER')
        artyPass = findProperty('ARTIFACTORY_PASS')
    }

    repositories {
        mavenCentral {
            credentials {
                username = System.getenv("SONATYPE_USERNAME") ?: findProperty('ARTIFACTORY_USER')
                password = System.getenv("SONATYPE_PASSWORD") ?: findProperty('ARTIFACTORY_PASS')
            }
        }
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
    id 'org.jetbrains.dokka' version '1.9.20'
    id 'com.apollographql.apollo' version '4.0.0'
    id 'maven-publish'
}

repositories {
    mavenCentral {
        credentials {
            username = System.getenv("SONATYPE_USERNAME") ?: findProperty('ARTIFACTORY_USER')
            password = System.getenv("SONATYPE_PASSWORD") ?: findProperty('ARTIFACTORY_PASS')
        }
    }
}

dependencies {
    dokkaHtmlPlugin 'org.jetbrains.dokka:versioning-plugin:1.9.20'

    implementation 'com.apollographql.apollo:apollo-runtime:4.0.0'
    implementation 'com.apollographql.ktor:apollo-engine-ktor:0.0.2'
//    implementation 'com.apollographql.adapters:apollo-adapters-core:0.0.4'
//    implementation 'com.apollographql.adapters:apollo-adapters-kotlinx-datetime:0.0.4'
    implementation 'org.slf4j:slf4j-simple:2.0.13'
    implementation 'io.ktor:ktor-client-core:2.3.10'
    implementation 'io.ktor:ktor-client-auth-jvm:2.3.10'
    implementation 'io.ktor:ktor-http:2.3.10'
    implementation 'io.ktor:ktor-client-okhttp:2.3.10'
    implementation 'io.ktor:ktor-client-content-negotiation-jvm:2.3.10'
    implementation 'io.ktor:ktor-serialization-jackson-jvm:2.3.10'
    implementation 'io.ktor:ktor-client-logging-jvm:2.3.10'
    implementation 'io.ktor:ktor-client-encoding:2.3.10'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
    implementation 'org.jetbrains.kotlinx:atomicfu-jvm:0.24.0'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'org.hibernate.validator:hibernate-validator:6.2.5.Final'
    implementation 'jakarta.validation:jakarta.validation-api:2.0.2'

}

kotlin {
    jvmToolchain(11)
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

subprojects {
    afterEvaluate { project ->
        if (project.name == System.getenv('EXCLUDE_MODULE')) {
            // Disable all tasks for this module
            tasks.configureEach { task ->
                task.enabled = false
            }
        }
    }
}

publishing {
    repositories {
        mavenCentral {
            credentials {
                username = System.getenv("SONATYPE_USERNAME")
                password = System.getenv("SONATYPE_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId project.name
            groupId project.property('groupId')
            version project.property('version')
            description project.property('description')
        }
    }
}

tasks.register('versionsSet') {
    doLast {
        def pattern = ~/(\R|^)(version\s*=\s*)([-.A-Z0-9]+)(\R|$)/
        String gradleProperties = file('gradle.properties').text
        String currentVersion = (gradleProperties =~ pattern)[0][3]
        println("Current version: $currentVersion")
        println("Project version: ${project.version}")
        if (project.version != currentVersion) {
            file('gradle.properties').text = gradleProperties.replaceFirst(pattern, "\$1\$2${project.version}\$4")
        }
    }
}

tasks.withType(DokkaTask.class) {

    moduleName.set("Lodging Connectivity Java SDK")

    String versioningConfiguration = """
    {
      "version": "${project.version}",
      "olderVersionsDir": "${project.properties["dokka-old-versions-dir"]}",
      "renderVersionsNavigationOnAllPages": true
    }
    """

    String dokkaBaseConfiguration = """
    {
        "customAssets": ["${file("assets/logo-icon.svg")}"],
        "customStyleSheets": ["${file("assets/custom-styles.css")}"]
    }
    """

    pluginsMapConfiguration.set(
            // fully qualified plugin name to json configuration
            [
                    "org.jetbrains.dokka.base.DokkaBase"             : dokkaBaseConfiguration,
                    "org.jetbrains.dokka.versioning.VersioningPlugin": versioningConfiguration
            ]
    )

    dokkaSourceSets {
        configureEach {
            sourceRoots.setFrom(
                    file("src/main/kotlin/com/expediagroup/sdk/lodgingconnectivity"),
                    file("src/main/java/generated")
            )

            // Suppress documentation for specific patterns using a consolidated regex
            perPackageOption {
                matchingRegex.set('.*com.expediagroup.sdk.lodgingconnectivity.graphql.(supply|payment|sandbox).(adapter|selections|fragment).*')
                suppress.set(true)
            }

            perPackageOption {
                matchingRegex.set('.*com.expediagroup.sdk.lodgingconnectivity.graphql.(supply|payment|sandbox).type.adapter.*')
                suppress.set(true)
            }
        }
    }
}

apollo {
    service("lodgingsupply") {
        generateInputBuilders.set(true)
        srcDir("src/main/graphql/lodgingsupply")

        packageName.set("com.expediagroup.sdk.lodgingconnectivity.graphql.supply")
    }
    service("payment") {
        generateInputBuilders.set(true)

        srcDir("src/main/graphql/payment")

        packageName.set("com.expediagroup.sdk.lodgingconnectivity.graphql.payment")
    }
    service("sandbox") {
        generateInputBuilders.set(true)

        srcDir("src/main/graphql/sandbox")

        packageName.set("com.expediagroup.sdk.lodgingconnectivity.graphql.sandbox")
    }
}
